{Author: Galkov A.V
 Group: 115
 Этап: 3}
program prog(input,output);
{**************************************************************************
Структура программы:
 1) процедура root поиска корня функции f(x);
 2) функция integral вычисления определенного интеграла функции f(x);
 3) основная программа подсчета и вывода всех значений.
**************************************************************************}

type TF=function(x:real):real;
var  S,x12,x13,x23:real;
{**************************************************************************
 S - площадь фигуры, ограниченной тремя кривыми, заданными функциями
     h1(x)=3*(0.5/(x+1)+1), h2(x)=5/x и h3(x)=2.5x-9.5;
 x12 - точка пересечения функций h1(x)=3*(0.5/(x+1)+1) и h2(x)=5/x
 x13 - точка пересечения функций h1(x)=3*(0.5/(x+1)+1) и h3(x)=2.5x-9.5
 x23 - точка пересечения функций h2(x)=5/x и h3(x)=2.5x-9.5
**************************************************************************}


{**************************************************************************
{Процедура root ищет корень функции f(x), используя комбинированный метод:
 одновременно используются метод хорд и метод касательных.
 В своей работе использует функции:
 f1(x:real):real - вычисляет в точке x значение функции f1(x)=h1(x)-h2(x),
                   где h1(x)=3*(0.5/(x+1)+1) и h2(x)=5/x;

 f2(x:real):real - вычисляет в точке x значение функции f1(x)=h1(x)-h3(x),
                   где h1(x)=3*(0.5/(x+1)+1) и h3(x)=2.5x-9.5;

 f3(x:real):real - вычисляет в точке x значение функции f1(x)=h3(x)-h2(x),
                   где h2(x)=5/x и h3(x)=2.5x-9.5;

 g1(x:real):real - вычисляет в точке x значение первой производной функции
                   f(x)=h1(x)-h2(x), где h1(x)=3*(0.5/(x+1)+1) и h2(x)=5/x;

 g2(x:real):real - вычисляет в точке x значение первой производной функции
                   f(x)=h1(x)-h3(x), h1(x)=3*(0.5/(x+1)+1) и
                   h3(x)=2.5x-9.5;

 g3(x:real):real;  вычисляет в точке x значение первой производной функции
                   f(x)=h3(x)-h2(x), h2(x)=5/x и h3(x)=2.5x-9.5;
****************************************************************************}

{$F+}
function f1(x:real):real;
begin
 f1:=3*(0.5/(x+1)+1)-5/x;
end;

{$F+}
function f2(x:real):real;
begin
 f2:=1.5/(x+1)-2.5*x+12.5;
end;

{$F+}
function f3(x:real):real;
begin
 f3:=2.5*x-9.5-5/x;
end;

{$F+}
function g1(x:real):real;
begin
 g1:=-1.5/sqr(x+1)+5/sqr(x);
end;

{$F+}
function g2(x:real):real;
begin
 g2:=-1.5/sqr(x+1)-2.5;
end;

{$F+}
function g3(x:real):real;
begin
 g3:=2.5+5/sqr(x)
end;

procedure root(f,g:TF; a,b,eps1:real;var x:real);
var c1,c2,help:real;
{*************************************************************************
 f,g - параметры-функции;
 [a,b] - отрезок, на котором процедура root ищет корень функции f(x)
 eps1 - точность нахождения корня функции f(x);
 x - корень функции f(x);
*************************************************************************}
begin
 if ( f(a)<0 ) and ( f( (a+b)/2 )<( f(a)+f(b) ) / 2) or
    ( f(a)>0 ) and ( f( (a+b)/2 )>( f(a)+f(b) ) / 2)
    then
    else begin
          help:=a;
          a:=b;
          b:=help;
         end;
 repeat
 {Нахождение новых граничных точек}
  c1:=(a*f(b)-b*f(a))/(f(b)-f(a));
  c2:=b-f(b)/g(b);
  a:=c1;
  b:=c2;
 until abs(b-a)<eps1;
 x:=(b+a)/2;
end;

{****************************************************************************
 Функция integral вычисляет величину определенного интеграла функции f(x).
 Вычисление осуществляется по формуле Симпсона.
 Заданная точность считается достигнутой, если модуль разности величин
 интегралов при количествах разбиений n и 2n, умноженный на 1/15
 меньше eps2.
 В своей работе использует функции:
 1) f1(x:real):real;
 2) f2(x:real):real.
****************************************************************************}

function integral(f:TF; a,b,eps2:real):real;
var n,i:integer;
    intn,int2n,s,p:real;
{****************************************************************************
 [a,b] - отрезок, на котором вычисляется значение интеграла функции f(x);
 eps2 - точность нахождения величины интеграла функции f(x);
 n - число разбиений
 intn - величина интеграла при n разбиений
 int2n - величина интеграла при 2n разбиений
****************************************************************************}

begin
 n:=1;                         { - начальное количество разбиений}
 int2n:=(f(a)+f(b))*(b-a)/3;   { - начальное значение интеграла}
 s:=0;                         { - переменная для подсчета промежуточных сумм}
 p:=(b-a)/n;       { - начальная длина отрезков, на которые разбивается [a,b]}
 repeat
  intn:=int2n;    { - присваиваем предыдущему значению интеграла более точное}
  int2n:=int2n*3/p - 2*s;      { - изменяем коэффициенты для подсчета более
                                   точного значения итеграла}
  s:=0;
  n:=n*2;                      { - увеличиваем число разбиений}
  p:=(b-a)/n;                  { - вычисляем новую длину отрезков}
  {Считаем сумму дополнительных значений функции}
  for i:=1 to n-1 do
                    begin
                     if i mod 2 =1 then s:=s + f(a+i*p);
                    end;

  int2n:=(int2n + 4*s)*p/3;    { - вычисляем более точное значение интеграла}
 until 1/15*abs(Intn-Int2n)<eps2;
 integral:=int2n;              { - окончательное значение интеграла}
end;

begin
 writeln('Абсцисcы точек пересечения функций:');
{Точки ищутся на отрезке [1,10] с точностью 0.001}

{Поиск точки пересечения функций h1(x)=3*(0.5/(x+1)+1) и h2(x)=5/x}
 root(f1,g1,1,10,0.001,x12);
 write('1) h1(x)=3*(0.5/(x+1)+1)  и  h2(x)=5/x:');
 writeln(x12:15:3);

{Поиск точки пересечения функций h1(x)=3*(0.5/(x+1)+1) и h3(x)=2.5x-9.5}
 root(f2,g2,1,10,0.001,x13);
 write('2) h1(x)=3*(0.5/(x+1)+1)  и  h3(x)=2.5x-9.5:');
 writeln(x13:10:3);

{Поиск точки пересечения функций h2(x)=5/x и h3(x)=2.5x-9.5}
 root(f3,g3,1,10,0.001,x23);
 write('3) h2(x)=5/x              и  h3(x)=2.5x-9.5:');
 writeln(x23:10:3);
 writeln;

{*************************************************************************
 Вычисляем с точность 0.001 площадь фигуры, ограниченной тремя кривыми,
 заданными функциями h1(x)=3*(0.5/(x+1)+1), h2(x)=5/x и h3(x)=2.5x-9.5,
 как сумму двух интегралов:
 1) определенный интеграл от функции f1(x)=h1(x)-h2(x)
 на отрезке [x12,x23], где h1(x)=3*(0.5/(x+1)+1) и h2(x)=5/x
 2) определенный интеграл от функции f2(x)=h1(x)-h3(x)
 на отрезке [x23,x13], где h1(x)=3*(0.5/(x+1)+1) и h3(x)=2.5x-9.5
*************************************************************************}

 S:=integral(f1,x12,x23,0.001) + integral(f2,x23,x13,0.001);
 writeln('Площадь фигуры, ограниченной тремя кривыми,');
 writeln('заданными функциями');
 writeln('h1(x)=3*(0.5/(x+1)+1),');
 writeln('h2(x)=5/x и');
 writeln('h3(x)=2.5x-9.5');
 writeln('приблизительно равна:',S:33:3);

{Даем пользователю возможность посмотреть результат}
 writeln('Нажмите Enter');
 readln
end.